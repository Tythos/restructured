DefinitionList =
  BlankLines?
  IsDefinitionListItem
  head:DefinitionListItem
  tail:(BlankLines? IsDefinitionListItem DefinitionListItem)*
  &(BlankLines) {
    var children = [head].concat(mapByIndex(tail, 2));
    return new Elements.DefinitionList({ children: children });
  }

IsDefinitionListItem =
  &(
    SameIndent !NormalizedToWhitespace RawLine
    DeepIndent !NormalizedToWhitespace RawLine
  )

DefinitionListItem =
  SameIndent
  term:DefinitionTerm
  classifiers:(Whitespace+ ':' Whitespace+ DefinitionClassifier)* Whitespace* Newline
  Indent
  body:BodyElement+
  Dedent {
    return new Elements.DefinitionListItem({
      term: term,
      classifiers: mapByIndex(classifiers, 3),
      definition: new Elements.Definition({ children: body }),
    });
  }

DefinitionTerm =
  children:(InlineMarkupWithoutNewline / DefinitionTermText)+ {
    return new Elements.Term({ children: children });
  }

DefinitionClassifierBeggining = Whitespace+ ':' Whitespace+

DefinitionTermText =
  text:(!Endline !DefinitionClassifierBeggining !InlineMarkupBeggining .)+ {
    return new Text({ text: mapByIndex(text, 3).join('')});
  }

DefinitionClassifier =
  term:(!(Endline / Whitespace+ ':') .)+ {
    var text = mapByIndex(term, 1).join('');
    return new Elements.Classifier({ children: [new Text({ text: text }) ]});
  }
