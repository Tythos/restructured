DefinitionList = items:(BlankLines? DefinitionListItem)+ &BlankLines {
  return new Elements.DefinitionList({ children: mapByIndex(items, 1) });
}

DefinitionListItem =
  SameIndent
  term:DefinitionTerm
  classifiers:(Whitespace+ ':' Whitespace+ DefinitionClassifier)* Whitespace* Newline
  PushFirstIndent$
  body:(BodyElement+ / FailbackIndent$)
  PopIndent$ {
    return new Elements.DefinitionListItem({
      term: term,
      classifiers: mapByIndex(classifiers, 3),
      definition: new Elements.Definition({ children: body }),
    });
  }

DefinitionTerm = children:(InlineMarkupWithoutNewline / DefinitionTermText)+ {
  return new Elements.Term({ children: children });
}

DefinitionClassifierBeggining = Whitespace+ ':' Whitespace+

DefinitionTermText = text:(!Endline !DefinitionClassifierBeggining !InlineMarkupBeggining .)+ {
  return new Text({ text: mapByIndex(text, 3).join('')});
}

DefinitionClassifier = children:(InlineMarkupWithoutNewline / DefinitionTermText)+ {
  return new Elements.Classifier({ children: children });
}
