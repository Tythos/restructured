DefinitionList = children:DefinitionListItem+ &BlankLines {
  return new Elements.DefinitionList({ children });
}

DefinitionListItem =
  blanklines:BlankLines?
  SameIndent
  term:DefinitionTerm
  classifiers:(Whitespace+ ':' Whitespace+ DefinitionClassifier)* Whitespace* Newline
  indent:PushFirstIndent$
  body:(DefinitionListBody / FailbackIndent$)
  PopIndent$ {
    var classifiers = mapByIndex(classifiers, 3);
    return new Elements.DefinitionListItem({
      blanklines,
      indent,
      children: [term, ...classifiers, body],
    });
  }

DefinitionTerm = children:(InlineMarkupWithoutNewline / DefinitionTermText)+ {
  return new Elements.Term({ children });
}

DefinitionListBody = children:BodyElement+ {
  return new Elements.Definition({ children });
}

DefinitionClassifierBeggining = Whitespace+ ':' Whitespace+

DefinitionTermText = text:(!Endline !DefinitionClassifierBeggining !InlineMarkupBeggining .)+ {
  return new Elements.Text({ value: mapByIndex(text, 3).join('') });
}

DefinitionClassifier = children:(InlineMarkupWithoutNewline / DefinitionTermText)+ {
  return new Elements.Classifier({ children });
}
