// Utility for Paragraph
InlineMarkupPreceding = c:. &{ return ParserUtil.isInlineMarkupPrefix(c); } { return c; }
InlineMarkupStartString = '**' / '*' / '``' / '`' / '|' / '_`'
InlineMarkupBeggining =
  p:InlineMarkupPreceding s:InlineMarkupStartString !Endline !Whitespace f:. &{
    return !(p === '*' && s === '*') && !ParserUtil.isMatchPunctuations(p, f);
  } / InlineMarkupPreceding (FootnoteReference /
                             CitationReference /
                             AnonymousSimpleHyperlinkReference /
                             NamedSimpleHyperlinkReference)

Paragraph = children:(SameIndent ParagraphBlock)+ &((LiteralBlockWithoutIndent  SkipIndentCheck$) / BlankLines / EndIndent) {
  return new Elements.Paragraph({ children: _.flatten(mapByIndex(children, 1)) });
}

ParagraphBlock =
  children:(
    !(BlankLines / LiteralBlockWithoutIndent)
    (InlineMarkup / ParagraphText)
  )+
  Whitespace*
  last:(Endline / &LiteralBlockWithoutIndent) {
    var texts = mapByIndex(children, 1);
    if (last) {
      if (texts[texts.length - 1].type !== 'text') {
        return texts.concat(new Text({ value: last }));
      }
      texts[texts.length - 1] = new Text({ value: texts[texts.length - 1].value + last });
    }
    return texts;
  }

ParagraphText =
  text:(!(Endline / LiteralBlockWithoutIndent) !InlineMarkupBeggining .)+ &(Endline / LiteralBlockWithoutIndent) {
    return new Text({ value: mapByIndex(text, 2).join('') });
  } / text:(!Endline !InlineMarkupBeggining .)* last:InlineMarkupPreceding {
    return new Text({ value: mapByIndex(text, 2).join('') + last });
  }

OnelineParagraph = (InlineMarkupWithoutNewline / OnelineText)+

OnelineText =
  text:(!Endline !InlineMarkupBeggining .)+ last:(!Endline InlineMarkupPreceding)? {
    var s = mapByIndex(text, 2).join('');
    if (!_.isNull(last)) { s += last[1]; }
    return new Text({ value: s });
  } / text:(!Endline !InlineMarkupBeggining .)* last:(!Endline InlineMarkupPreceding) {
    var s = mapByIndex(text, 2).join('') + last[1];
    return new Text({ value: s });
  }
