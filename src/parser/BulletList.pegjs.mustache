BulletList =
  BlankLines?
  &(SameIndent b:Bullet NormalizedToWhitespace &{ bulletListState.pushBullet(b); return true; })
  head:BulletListItem
  tail:(BlankLines? BulletListItem)* {
    bulletListState.popBullet();
    return new Elements.BulletList({ children: [head].concat(mapByIndex(tail, 1)) });
  }

Bullet = [*+\-•‣⁃]

BulletListItem =
  SameIndent
  b:Bullet
  &{ return bulletListState.isSameBullet(b); }
  body:(BulletListItemBody / EmptyBulletListItemBody) {
    return new Elements.ListItem({ children: body })
  }

BulletListItemBody = (BulletListIndent body:BodyElement* Dedent) { return body; }
EmptyBulletListItemBody = Whitespace* Endline { return []; }

BulletListIndent =
  i:Whitespace+ &(!NormalizedToWhitespace .) {
    indentState.push(currentIndentSize + ParserUtil.calcIndentSize(i) + 1);
    indentState.ignore(location().end.line);
  } /
  (Whitespace* Newline)+ &DeepIndent i:Whitespace+ &(!NormalizedToWhitespace .) {
    indentState.push(ParserUtil.calcIndentSize(i));
    indentState.ignore(location().end.line);
  }
