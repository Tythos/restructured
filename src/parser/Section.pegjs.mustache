Section =
  BlankLines?
  title:(SectionWithOverline / SectionWithoutOverline)
  BlankLines?
  children:(!IsSection (Transition / BodyElement))* {
    return new Elements.Section({ title: title, children: mapByIndex(children, 1) });
  }

SectionWithOverline =
  &(SectionLine !BlankLines RawLine SectionLine)
  overline:SectionLine
  Whitespace* children:OnelineParagraph Newline
  underline:SectionLine {
    return new Elements.SectionTitle({ children: children });
  }

IsSection = &(BlankLines SectionLine !BlankLines RawLine SectionLine) /
  &(BlankLines RawLine SectionLine)

SectionWithoutOverline =
  &(!BlankLines RawLine SectionLine)
  children:OnelineParagraph Newline
  underline:SectionLine {
    return new Elements.SectionTitle({ children: children });
  }

SectionLine =
  line:('!' / '"' / '#' / '$' / '%' / '&'/ "'" / '(' / ')' /
        '*' / '+' / ',' / '-' / '.' / '/' / ':' / ';' / '<' /
        '=' / '>' / '?' / '@' / '[' / '\\' / ']' / '^' / '_' /
        '`' / '{' / '|' / '}' / '~')+ &{
    return line.length >= 4 && _.uniq(line).length == 1
  } Whitespace* Newline {
    return {line: line.join('')}
  }
