Section =
  BlankLines?
  section:(SectionWithOverline / SectionWithoutOverline)
  &BlankLines {
    return section;
  }

SectionWithOverline =
  &(SectionLine !BlankLines RawLine SectionLine)
  overline:SectionLine
  Whitespace* children:OnelineParagraph Newline
  underline:SectionLine {
    return new Elements.Section({ children: children });
  }

SectionWithoutOverline =
  &(!BlankLines RawLine SectionLine)
  children:OnelineParagraph Newline
  underline:SectionLine {
    return new Elements.Section({ children: children });
  }

SectionLine =
  line:('!' / '"' / '#' / '$' / '%' / '&'/ "'" / '(' / ')' /
        '*' / '+' / ',' / '-' / '.' / '/' / ':' / ';' / '<' /
        '=' / '>' / '?' / '@' / '[' / '\\' / ']' / '^' / '_' /
        '`' / '{' / '|' / '}' / '~')+ &{
    return line.length >= 4 && _.uniq(line).length == 1
  } Whitespace* Newline {
    return {line: line.join('')}
  }
