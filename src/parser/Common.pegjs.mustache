AlphanumericAscii = [A-Za-z0-9]
Eof = !.
Newline = '\n' / ('\r' '\n'?)
Whitespace = ' ' / '\v' / '\f' / '\t'
Endline = Newline / Eof
NormalizedToWhitespace = Whitespace / Newline
Nonspacechar = c:(!NormalizedToWhitespace .) { return c[1]; }

BlankLines =
  (Whitespace* Newline &(Whitespace* Endline))*
  Whitespace*
  Endline

RawLine =
  raw:(!Endline .)* Endline {
    return { raw: _.map(raw, function (v) { return v[1]; }).join('') };
  }

NotEmptyRawLine =
  raw:(!Endline .)+ Endline {
    return { raw: _.map(raw, function (v) { return v[1]; }).join('') };
  }

Indent =
  &(i:Whitespace+ &{
    var size = ParserUtil.calcIndentSize(i);
    if (size <= currentIndentSize) { return false; }
    nextIndentSize = size;
    return true;
  })
  &((Whitespace* Newline / i:Whitespace+ RawLine &{
    var size = ParserUtil.calcIndentSize(i);
    if (size <= currentIndentSize) { return false; }
    nextIndentSize = Math.min(nextIndentSize, size);
    return true;
  })*)
  &{
    indentSizeStack.push(currentIndentSize);
    currentIndentSize = nextIndentSize;
    return true;
  }

Dedent =
  &{
    currentIndentSize = indentSizeStack.pop();
    return true;
  }

EndIndent = &(
  BlankLines?
  i:Whitespace* !NormalizedToWhitespace . &{
    return ParserUtil.calcIndentSize(i) < currentIndentSize
  })

SameIndent =
  i:Whitespace* &{
    var ignore = indentIgnoreLine === location().start.line;
    return ignore || ParserUtil.calcIndentSize(i) === currentIndentSize;
  } {
    return i.join('');
  }

DeepIndent = i:Whitespace* &{ return ParserUtil.calcIndentSize(i) > currentIndentSize; }
ShallowIndent = i:Whitespace* &{ return ParserUtil.calcIndentSize(i) < currentIndentSize; }
SameOrDeepIndent = i:Whitespace* &{ return ParserUtil.calcIndentSize(i) >= currentIndentSize; } {
  return i.join('');
}

Fail$ = &{ return false; }

PopIndent$ = &{
  currentIndentSize = indentSizeStack.pop();
  return true;
}

FailbackIndent$ = PopIndent$ Fail$

PushCommonIndent$ =
  &DeepIndent
  &(
    w1:Whitespace+ RawLine &{
      nextIndentSize = ParserUtil.calcIndentSize(w1);
      return true;
    }
    (
      (Whitespace* Newline) /
        (&DeepIndent w1:Whitespace+ RawLine &{
          nextIndentSize = Math.min(nextIndentSize, ParserUtil.calcIndentSize(w1));
          return true;
        })
    )* &{
      indentSizeStack.push(currentIndentSize);
      currentIndentSize = nextIndentSize;
      return true;
    }
  ) {
    return { indentSize: currentIndentSize };
  }
