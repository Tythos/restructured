BlockQuote = NestedBlockQuote / SimpleBlockQuote

NestedBlockQuote =
  blanklines:BlankLines?
  indent:PushFirstIndent$
    inner:InnerBlockQuote
    &(BlankLines ShallowIndent / FailbackIndent$)
  PopIndent$
  outer:BlockQuote {
    var innerIndent = { width: indent.width, offset: indent.width - outer.indent.width };
    inner.indent = innerIndent;

    var children = [inner].concat(outer.children);
    var indent = outer.indent;
    return new Elements.BlockQuote({ indent, blanklines, children });
  }

InnerBlockQuote =
  nested:(BlockQuoteBody / FailbackIndent$)
  attribution:Attribution? {
  return new Elements.BlockQuote({ children: nested.concat(attribution || []) });
}

SimpleBlockQuote =
  blanklines:BlankLines?
  indent:PushFirstIndent$
    children:(BlockQuoteBody / FailbackIndent$)
    attribution:Attribution?
  PopIndent$ {
    return new Elements.BlockQuote({
      indent,
      blanklines,
      children: children.concat(attribution || []),
    });
  }

BlockQuoteBody =
  head:BodyElementExceptBlockQuote
  tail:(!Attribution BodyElement)* {
    return [head].concat(_.map(tail, function (v) { return v[1]; }));
  }

Attribution =
  blanklines:BlankLines
  SameIndent ('---' / '--' / '\u2014') Whitespace* &(!Endline .)
  body:(AttributionBodyWithIndent / AttributionBodyWithoutIndent) {
    return new Elements.Attribution({ blanklines, children: body.children });
  }

PushAttributionIndent$ = &(RawLine PushFirstIndent$)

AttributionBodyWithIndent = PushAttributionIndent$ SkipIndentCheck$ body:(Paragraph / FailbackIndent$) PopIndent$ {
  return body;
}

AttributionBodyWithoutIndent = SkipIndentCheck$ body:Paragraph { return body; }
